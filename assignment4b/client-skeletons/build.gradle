// Required by Vanderbilt grader - do not remove
buildscript {
	repositories { maven { url uri('plugin') } }
	dependencies { classpath 'edu.vanderbilt.grader:gradle-plugin:3.0.6.0' }
}

buildscript {
    ext {
        // Needs to be an extension for functions to be able to access it.

        versionMajor = 1
        versionMinor = 2
        versionPatch = 0

        build_versions = [
                compileSdk: 30,
                targetSdk : 30,
                minSdk    : 30,
                java      : JavaVersion.VERSION_11,
                code      : versionMajor * 10000 + versionMinor * 100 + versionPatch,
                name      : "${versionMajor}.${versionMinor}.${versionPatch}"
        ]

        versions = [
                kotlin           : '1.5.31',
                coroutines       : '1.5.1',
                support          : '1.2.1',
                annotation       : '1.2.0',
                legacy_support   : '1.0.0',
                appcompat        : '1.3.0',
                material         : '1.4.0',
                preference_ktx   : '1.1.1',
                constraint_layout: '2.0.4',
                arch_components  : '2.3.1',
                core_ktx         : '1.6.0',
                viewmodel_ktx    : '2.3.1',
                glide            : '4.12.0',
                picasso          : '2.71828',
                gson             : '2.8.7',
                annotations      : '21.0.1',
                commons_io       : '20030203.000550',
                jsoup            : '1.14.1',
                leak_canary      : '2.7',
                reactor          : '3.4.7',
                rxjava           : '3.0.13',
                rxandroid        : '3.0.0',
                rxbinding        : '2.2.0',
                espresso         : '3.4.0',
                okhttp           : '4.12.0',
                junit            : '4.13.2',
                mockito          : '3.11.2',
                mockito_kotlin   : '2.2.0'
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

plugins {
    id "org.jetbrains.kotlin.kapt" version "1.5.31" apply false
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    afterEvaluate {
        if (plugins.findPlugin('android') ?: plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion build_versions.compileSdk

                defaultConfig {
                    minSdkVersion build_versions.minSdk
                    targetSdkVersion build_versions.compileSdk
                }

                compileOptions {
                    sourceCompatibility build_versions.java
                    targetCompatibility build_versions.java
                }

                kotlinOptions {
                    jvmTarget = '11'
                }
            }
        }
    }
}

if (file("$projectDir/admin/skeleton.gradle").isFile()) {
    apply from: "$projectDir/admin/skeleton.gradle"
}

// Required by Vanderbilt grader - do not remove
subprojects { proj ->
	afterEvaluate {
		if (proj.plugins.findPlugin('android') ?: proj.plugins.findPlugin('android-library')) {
			proj.apply plugin: 'edu.vanderbilt.grader.plugin'
		}
	}
}
