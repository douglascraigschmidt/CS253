apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    defaultConfig {
        applicationId "edu.vanderbilt.androidimagecrawler"
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        main {
            file('src/main/res-screen')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }


    // Androidx test
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    // Shows nicer terminal output when running tests through gradle on the command line.
    testOptions.unitTests.all {
        //For Junit5: useJUnitPlatform()
        testLogging {
            events 'failed' //,'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility build_versions.java
        targetCompatibility build_versions.java
    }

    lint {
        abortOnError false
    }
    namespace 'edu.vanderbilt.crawler'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':image-crawler')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation "androidx.annotation:annotation:$versions.annotation"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.recyclerview:recyclerview:$versions.support"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint_layout"

    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.arch_components"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.viewmodel_ktx"
    implementation "androidx.preference:preference-ktx:$versions.preference_ktx"
    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation 'androidx.fragment:fragment-ktx:1.5.2'
    implementation "com.google.android.material:material:$versions.material"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    implementation "com.squareup.picasso:picasso:$versions.picasso"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation "com.github.bumptech.glide:okhttp-integration:$versions.okhttp"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"
    implementation "com.google.code.gson:gson:$versions.gson"
    implementation "commons-io:commons-io:$versions.commons_io"
    implementation "io.reactivex.rxjava3:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava3:rxandroid:$versions.rxandroid"
    implementation "com.jakewharton.rxbinding2:rxbinding:$versions.rxbinding"
    implementation 'com.facebook.stetho:stetho:1.6.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leak_canary"

    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$versions.kotlin"

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Expresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}
