// Required by Vanderbilt grader - do not remove
buildscript {
	repositories { jcenter() }
	dependencies { classpath 'edu.vanderbilt.grader:gradle-plugin:3.0.2' }
}

buildscript {
    ext {
        // Needs to be an extension for functions to be able to access it.

        versionMajor = 1
        versionMinor = 2
        versionPatch = 0

        build_versions = [
                compileSdk: 30,
                targetSdk : 30,
                minSdk    : 29,
                java      : JavaVersion.VERSION_1_8,
                code      : versionMajor * 10000 + versionMinor * 100 + versionPatch,
                name      : "${versionMajor}.${versionMinor}.${versionPatch}"
        ]

        versions = [
                android          : '4.1.1',
                kotlin           : '1.4.10',
                coroutines       : '1.3.7',
                support          : '1.1.0',
                annotation       : '1.1.0',
                legacy_support   : '1.0.0',
                appcompat        : '1.2.0',
                material         : '1.2.1',
                preference_ktx   : '1.1.1',
                constraint_layout: '2.0.1',
                arch_components  : '2.2.0',
                core_ktx         : '1.3.1',
                viewmodel_ktx    : '2.2.0',
                glide            : '4.11.0',
                picasso          : '2.71828',
                gson             : '2.8.6',
                annotations      : '13.0',
                commons_io       : '20030203.000550',
                jsoup            : '1.13.1',
                leak_canary      : '2.4',
                reactor          : '3.3.4.RELEASE',
                rxjava           : '2.2.19',
                rxandroid        : '2.1.1',
                rxbinding        : '2.2.0',
                espresso         : '3.3.0',
                okhttp           : '4.11.0',
                junit            : '4.13',
                mockito          : '3.5.10',
                mockito_kotlin   : '2.2.0'
        ]
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        if (file("$projectDir/admin/skeleton.gradle").isFile()) {
            classpath "org.ajoberstar:grgit:1.9.3"
        }
    }
}

plugins {
    id "org.jetbrains.kotlin.kapt" version "1.4.10" apply false
}

// This is an example of how to ensure that a project with multiple android
// sub-modules will use the same android plugin settings and versions. This
// project only has a single Android module so this is not really necessary
// here, but it also doesn't hurt.
subprojects {
    repositories {
        google()
        jcenter()
    }

    afterEvaluate {
        if (plugins.findPlugin('android') ?: plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion build_versions.compileSdk

                defaultConfig {
                    minSdkVersion build_versions.minSdk
                    targetSdkVersion build_versions.compileSdk
                }

                compileOptions {
                    sourceCompatibility build_versions.java
                    targetCompatibility build_versions.java
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }

            androidExtensions {
                experimental true
            }
        }
    }
}

if (file("$projectDir/admin/skeleton.gradle").isFile()) {
    apply from: "$projectDir/admin/skeleton.gradle"
}

// Required by Vanderbilt grader - do not remove
subprojects { proj ->
	proj.repositories { jcenter() }
	afterEvaluate {
		if (!proj.plugins.findPlugin('android')) {
			proj.apply plugin: 'edu.vanderbilt.grader.plugin'
			proj.dependencies { testImplementation 'edu.vanderbilt.grader:rubric:1.1' }
		}
	}
}
